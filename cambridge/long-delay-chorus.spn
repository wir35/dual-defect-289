; Long delay program
; Summed mono input with separately adjustable left and right taps.
; This variation adds randomized chorus to the delay line.

; There is no digital feedback in this program.
; Use analog feedback, preferably to the right side.
; Left side: delay input, right side: feedback input.
; Unity gain through the delay line.
; The two taps can be modulated, but the read is not interpolated so it is noisy.

; Controls:
; Pot 0: Left tap time
; Pot 1: Right tap time
; Pot 2: Chorus depth

; Memory declarations

  MEM del1 30719    ; Main delay mem
  MEM mod_l 1023    ; Modulation left
  MEM mod_r 1023    ; Modulation right

; Constants

  EQU delay_size   0.875       ; Delay size factor
  EQU smooth       0.005       ; Pot 0 and 1 low pass smoothing filter
  EQU full_scale   32767 * 256 ; 0x7FFF00
  EQU make_up_gain -1.414      ; 2x
  EQU input_gain   0.5

; Registers and intermediate nodes

  EQU scaled_pot0      reg1  ;  Left pre-delay control
  EQU scaled_pot1      reg2  ;  Right pre-delay control
  EQU scaled_pot2      reg3  ;  Chorus depth

; Init program

  SKP	run, loop
  WLDS sin0, 200, 16384     ; LFO rates will be randomized
  WLDS sin1, 200, 16384     ;
  WLDR rmp0, 4123, 4096     ; Ramp for "random" value to be sampled
  CLR

loop:

; Left tap time set by pot0
  RDAX pot0, delay_size     ; Load pot0
  RDFX scaled_pot0, smooth  ; Low pass smooth it
  WRAX scaled_pot0, 0.0     ; Save scaled pot, clear ACC

; Right tap time by pot1
  RDAX pot1, delay_size     ; Load pot1
  RDFX scaled_pot1, smooth  ; Low pass smooth it
  WRAX scaled_pot1, 0.0     ; Save scaled pot, clear ACC

; Chorus depth set by pot2
  RDAX pot2, 1.0
  MULX pot2                 ; Cubic shape it
  MULX pot2
  SOF	 0.01, 0              ; Scale down to a reasonable range
  WRAX SIN0_RANGE, 1.0
  WRAX SIN1_RANGE, 0.0

; Input and write the mix to delay head

  RDAX adcl, input_gain
  RDAX adcr, input_gain
  WRA del1, 0.0

; Left output, delay tap time swept by pot 0

  OR   full_scale          ; Load ACC with 0x7FFF00
  MULX scaled_pot0         ; Scale by the smoothed pot0 value
  WRAX addr_ptr, 0         ; Set addr_ptr register for tap time
  RMPA 1.0                 ; Read delay memory
  WRA  mod_l, 0            ; Write to modulation buffer

; Left chorus

  CHO RDA, sin0, SIN | REG | COMPC, mod_l + 300
  CHO	RDA, sin0, 0, mod_l + 301
  WRA mod_l + 700, 0.0

; Left output to dac from end of modulation buffer

  RDA  mod_l#, 1.0
  SOF  make_up_gain, 0.0    ; Scale for make-up gain
  SOF  make_up_gain, 0.0
  WRAX dacl, 0.0            ; Write to dac left output

; Right output, delay tap time swept by pot 1

  OR   full_scale
  MULX scaled_pot1
  WRAX addr_ptr, 0
  RMPA 1.0
  WRA  mod_r, 0

; Right chorus

  CHO RDA, sin1, SIN | REG | COMPC, mod_r + 300
  CHO	RDA, sin1, 0, mod_r + 301
  WRA mod_r + 700, 0.0

; Right output to dac from end of modulation buffer

  RDA  mod_r#, 1.0
  SOF  make_up_gain, 0.0
  SOF  make_up_gain, 0.0
  WRAX dacr, 0.0

; Alternately update LFO 0 and 1 rates
  CHO  RDAL, sin0
  SKP  GEZ,  update_lfo1

update_lfo0:
  CLR
  CHO  RDAL, rmp0
  RDAX SIN0_RATE, 0.01
  WRAX SIN0_RATE, 0.0

update_lfo1:
  CLR
  CHO  RDAL, rmp0
  RDAX SIN1_RATE, 0.01
  WRAX SIN1_RATE, 0.0
