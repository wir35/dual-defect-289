; Inspired by the concert hall algorithm from lexicon 224xl.
; Topology here: http://freeverb3vst.osdn.jp/doc/Progenitor.jpg

; Variation: low diffusion, sparse echos, large sized space.

; Memory Declarations

  MEM lap1  346
  MEM lap1d 2
  MEM lap2  568
  MEM ld    1529 + 2818 + 887 + 498 + 1957 + 1183 + 1832 + 2279

  MEM rap1  297
  MEM rap1d 2
  MEM rap2  477
  MEM rd    2117 + 2946 + 533 + 725 + 2117 + 997 + 1943 + 23

; Lengths for nested all passes

  EQU lap3  1529                ; start point inside ld
  EQU lap3a 2818 + 887          ;
  EQU lap3b 2818                ; inside lap3_a

  EQU lap4  5734                ; start point inside ld
  EQU lap4a 1957 + 1183 + 1832  ;
  EQU lap4b 1957 + 1183         ; inside lap4_a
  EQU lap4c 1957                ; inside lap4_b

  EQU rap3  2117                ; start point inside rd
  EQU rap3a 2946 + 533          ;
  EQU rap3b 2946                ; inside rap3_a

  EQU rap4  6322                ; start point inside rd
  EQU rap4a 2117 + 997 + 603
  EQU rap4b 2117 + 997          ; inside rap4_a
  EQU rap4c 2117                ; inside rap4_b

; Constants

  EQU	k_1kHz	0.174549
  EQU	k_2kHz	0.318630
  EQU	k_4kHz	0.535735
  EQU	k_8kHz  0.784458
  EQU	k_12kHz 0.899931

  EQU input_gain    0.125
  EQU make_up_gain -1.7
  EQU lfo_lpf       0.01

; Program diffusion settings

  EQU diffusion1 0.1   ; Input diffusors
  EQU diffusion2 0.05   ; Outer all passes
  EQU diffusion3 0.03   ; Inner all passes

; Registers

  EQU temp   reg0
  EQU	llpf1	 reg1
  EQU	llpf2	 reg2
  EQU	rlpf1	 reg3
  EQU rlpf2  reg4

  EQU	rt	   reg5
  EQU lpf_cf reg7
  EQU lhpf   reg8
  EQU rhpf   reg9
  EQU hpf_cf reg10

; Init program

  SKP	run, loop
  WLDS sin0, 200, 100      ; Sin LFO rates are randomized
  WLDS sin1, 200, 100      ;
  WLDR rmp0, 4023, 4096    ; Ramps for random values
  WLDR rmp1, 6692, 4096    ;

loop:

; Prepare pot0 for reverb time (rt)

  LDAX pot0
  LOG	0.5, 0
  EXP	1.0, 0
  WRAX rt, 0.0

; Prepare pot1 for low pass cutoff (~1k to ~7k)

  LDAX pot1
  SOF  0.7, 0.2
  WRAX lpf_cf, 0.0

; Pot 1 for hpf coefficient

  LDAX pot1
  SOF  0.3, 0
  WRAX hpf_cf, 0.0

; Chorus depth set by pot2
  RDAX pot2, 1.0
  MULX pot2
  SOF 0.01, 0.0
  WRAX SIN0_RANGE, 1.0
  WRAX SIN1_RANGE, 0.0

; Left input with filters

  RDA  rd#, 1.0           ; Right loop feedback
  MULX rt                 ; Scale for reverb time
  RDAX adcl,  input_gain  ; Left input

  RDAX llpf1, -1.0        ; Adjustable lpf
  MULX lpf_cf
  RDAX llpf1,  1.0
  WRAX llpf1,  1.0

  SOF  -1, 0              ; High pass shelf
  RDFX lhpf, 0.1
  WRLX lhpf, -1
  MULX hpf_cf
  RDAX llpf1, 1.0

; Left diffusors into all pass loop

  RDA  lap1#, -diffusion1
  WRAP lap1,	 diffusion1
  RDA  lap2#, -diffusion1
  WRAP lap2,	 diffusion1
  WRA  ld,     0.0

; Right input with filters

  RDA  ld#, 1.0            ; Left loop feedback
  MULX rt                  ; Scale for reverb time
  RDAX adcr,  input_gain   ; Right input

  RDAX rlpf1, -1.0         ; Adjustable lpf
  MULX lpf_cf
  RDAX rlpf1, 1.0
  WRAX rlpf1, 1.0

  SOF  -1, 0              ; High pass shelf
  RDFX rhpf, 0.1
  WRLX rhpf, -1
  MULX hpf_cf
  RDAX rlpf1, 1.0

; Right diffusors into all pass loop

  RDA  rap1#, -diffusion1
  WRAP rap1,	 diffusion1
  RDA  rap2#, -diffusion1
  WRAP rap2,	 diffusion1
  WRA  rd,    0.0

; Nested all passes on left loop

  RDA   ld + lap3 + lap3a,  1.0
  RDA   ld + lap3, diffusion1
  WRAP  ld + lap3 + lap3a, -diffusion1
  WRA   ld + lap3, 0.0

  RDA   ld + lap3 + 1 + lap3b,  1.0
  RDA   ld + lap3 + 1, diffusion1
  WRAP  ld + lap3 + 1 + lap3b, -diffusion1
  WRA   ld + lap3 + 1, 0.0

; More nested all passes on left loop

  RDA   ld + lap4 + lap4a,  1.0
  RDA   ld + lap4, diffusion2
  WRAP  ld + lap4 + lap4a, -diffusion2
  WRA   ld + lap4, 0.0

  RDA   ld + lap4 + 1 + lap4b,  1.0
  RDA   ld + lap4 + 1, diffusion3
  WRAP  ld + lap4 + 1 + lap4b, -diffusion3
  WRA   ld + lap4 + 1, 0.0

  ; This one is chorused
  RDA   ld + lap4 + 2 + lap4c,  1.0
  RDA   ld + lap4 + 2, diffusion3
  WRAP  ld + lap4 + 2 + lap4c, -diffusion3
  WRA   ld + lap4 + 2, 0.0

; Nested all passes on right loop

  RDA   rd + rap3 + rap3a,  1.0
  RDA   rd + rap3, diffusion1
  WRAP  rd + rap3 + rap3a, -diffusion1
  WRA   rd + rap3, 0.0

  RDA   rd + rap3 + 1 + rap3b,  1.0
  RDA   rd + rap3 + 1, diffusion1
  WRAP  rd + rap3 + 1 + rap3b, -diffusion1
  WRA   rd + rap3 + 1, 0.0

; More nested all passes on right loop

  RDA   rd + rap4 + rap4a,  1.0
  RDA   rd + rap4, diffusion2
  WRAP  rd + rap4 + rap4a, -diffusion2
  WRA   rd + rap4, 0.0

  RDA   rd + rap4 + 1 + rap4b,  1.0
  RDA   rd + rap4 + 1, diffusion2
  WRAP  rd + rap4 + 1 + rap4b, -diffusion2
  WRA   rd + rap4 + 1, 0.0

  ; This one is chorused
  RDA   rd + rap4 + 2 + rap4c,  1.0
  RDA   rd + rap4 + 2, diffusion3
  WRAP  rd + rap4 + 2 + rap4c, -diffusion3
  WRA   rd + rap4 + 2, 0.0

; Alternately update LFO 0 and 1 rates
  CHO  RDAL, rmp0
  SOF -1, 0
  CHO  RDAL, rmp1
  SKP  GEZ,  update_lfo1

update_lfo0:
  CLR
  CHO  RDAL, rmp0
  SOF  0.2, 0.07
  RDAX SIN0_RATE, 0.01
  WRAX SIN0_RATE, 0.0
  SKP 0, chorus

update_lfo1:
  CLR
  CHO  RDAL, rmp1
  SOF  0.2, 0.07
  RDAX SIN1_RATE, 0.01
  WRAX SIN1_RATE, 0.0

chorus:

; Add chorus inside most nested all pass lap4c

  CHO RDA, sin0, SIN | REG | COMPC, ld + lap4 + 300
  CHO	RDA, sin0, 0,  ld + lap4 + 301
  WRA ld + lap4 + 700, 0

; Add chorus inside most nested all pass rap4c

  CHO RDA, sin1, SIN | REG | COMPC, rd + rap4 + 300
  CHO	RDA, sin1, 0,   rd + rap4 + 301
  WRA rd + rap4 + 700, 0

output:

  RDA  ld + 1276, 1.0
  RDA  rd#, 1.0
  SOF  make_up_gain, 0.0
  SOF  make_up_gain, 0.0
  SOF  make_up_gain, 0.0
  WRAX dacl, 0.0

  RDA  rd + 24, 1.0
  RDA  ld#, -1.0
  SOF  make_up_gain, 0.0
  SOF  make_up_gain, 0.0
  SOF  make_up_gain, 0.0
  WRAX dacr, 0.0
